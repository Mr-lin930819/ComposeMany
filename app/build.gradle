plugins {
    id 'com.android.application'
    id 'org.jetbrains.kotlin.android'
    id 'kotlin-kapt'
    id 'dagger.hilt.android.plugin'
    //protobuf生成
    id "com.google.protobuf" version "0.8.12"
    //自动生成parcelable
    id 'kotlin-parcelize'
    //navigation 安全类型数据
    id "androidx.navigation.safeargs.kotlin"
    id 'com.bytedance.android.aabResGuard'
}

android {
    compileSdkVersion 32

    defaultConfig {
        applicationId "com.mrlin.composemany"
        minSdkVersion 21
        targetSdkVersion 32
        versionCode 1
        versionName "1.0.0"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        vectorDrawables {
            useSupportLibrary true
        }

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += [
                        "room.schemaLocation"  : "$projectDir/schemas".toString(),
                        "room.incremental"     : "true",
                        "room.expandProjection": "true"]
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        debug {
            signingConfig signingConfigs.debug
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    buildFeatures {
        compose true
    }
    composeOptions {
        kotlinCompilerExtensionVersion compose_compiler_version
    }
    packagingOptions {
        resources {
            excludes += '/META-INF/{AL2.0,LGPL2.1}'
        }
    }
    signingConfigs {
        debug {
            storeFile file('../ks/composemany.jks')
            storePassword 'composemany'
            keyAlias 'many'
            keyPassword 'composemany'
        }
    }
}

dependencies {

    implementation 'androidx.core:core-ktx:1.8.0'
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    implementation "androidx.compose.ui:ui:$compose_version"
    implementation "androidx.compose.material:material:$compose_version"
    implementation "androidx.compose.ui:ui-tooling:$compose_version"
    //compose livedata扩展
    implementation "androidx.compose.runtime:runtime-livedata:$compose_version"
    //compose viewModel扩展
    implementation "androidx.lifecycle:lifecycle-viewmodel-compose:2.5.1"
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.5.1'
    implementation 'androidx.activity:activity-compose:1.5.1'
    //fragment
    implementation "androidx.fragment:fragment-ktx:$fragment_version"
    //navigation
    implementation "androidx.navigation:navigation-fragment-ktx:$nav_version"
    implementation "androidx.navigation:navigation-ui-ktx:$nav_version"
    implementation "androidx.navigation:navigation-compose:$nav_version"
    implementation "androidx.hilt:hilt-navigation-compose:1.0.0-alpha03"
    //hilt
    implementation "com.google.dagger:hilt-android:$hilt_android_version"
    kapt "com.google.dagger:hilt-compiler:$hilt_android_version"
    //ROOM
    def room_version = "2.4.3"
    implementation "androidx.room:room-runtime:$room_version"
    implementation "androidx.room:room-ktx:$room_version"
    kapt "androidx.room:room-compiler:$room_version"
    //datastore
    implementation "androidx.datastore:datastore:1.0.0"
    implementation "com.google.protobuf:protobuf-javalite:3.18.0"
    //splashscreen
    implementation "androidx.core:core-splashscreen:1.0.0-alpha02"
    //paging
    implementation "androidx.paging:paging-compose:1.0.0-alpha14"
    //coil
    implementation('io.coil-kt:coil-compose:1.4.0')
    implementation "androidx.compose.ui:ui-tooling-preview:$compose_version"
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    androidTestImplementation "androidx.compose.ui:ui-test-junit4:$compose_version"

    def accompanist_version = "0.25.1"
    //accompanist - pager
    implementation "com.google.accompanist:accompanist-pager:$accompanist_version"
    implementation "com.google.accompanist:accompanist-pager-indicators:$accompanist_version"
    implementation "com.google.accompanist:accompanist-systemuicontroller:$accompanist_version"
    implementation "com.google.accompanist:accompanist-placeholder-material:$accompanist_version"
    implementation "com.google.accompanist:accompanist-swiperefresh:$accompanist_version"
    //retrofit
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    //折叠工具栏
    implementation "me.onebone:toolbar-compose:2.3.4"
    implementation "br.com.devsrsouza.compose.icons.android:font-awesome:1.0.0"
    //decompose
    implementation "com.arkivanov.decompose:extensions-compose-jetpack:0.8.0"
}

kapt {
    correctErrorTypes true
    javacOptions {
        // These options are normally set automatically via the Hilt Gradle plugin, but we
        // set them manually to workaround a bug in the Kotlin 1.5.20
        option("-Adagger.fastInit=ENABLED")
        option("-Adagger.hilt.android.internal.disableAndroidSuperclassValidation=true")
    }
}

protobuf {
    protoc {
        artifact = "com.google.protobuf:protoc:3.10.0"
    }

    // Generates the java Protobuf-lite code for the Protobufs in this project. See
    // https://github.com/google/protobuf-gradle-plugin#customizing-protobuf-compilation
    // for more information.
    generateProtoTasks {
        all().each { task ->
            task.builtins {
                java {
                    option 'lite'
                }
            }
        }
    }
}

aabResGuard {
    whiteList = [ // White list rules
                  "*.R.raw.*",
                  "*.R.drawable.icon",
                  "*.R.mipmap.ic_launcher",
                  "*.R.mipmap.logo",
                  "*.R.string.default_web_client_id",
                  "*.R.string.firebase_database_url",
                  "*.R.string.gcm_defaultSenderId",
                  "*.R.string.google_api_key",
                  "*.R.string.google_app_id",
                  "*.R.string.google_crash_reporting_api_key",
                  "*.R.string.google_storage_bucket",
                  "*.R.string.project_id",
                  "*.R.string.com.crashlytics.android.build_id",
                  "*.R.string.tt_*",
                  "*.R.layout.tt_*",
                  "*.R.drawable.tt_*",
                  "*.R.layout.notification_*",
                  "*.R.string.star_*",
                  "*.R.dimen.tt_*",
                  "*.R.integer.tt_*",
                  "*.R.anim.tt_*",
                  "*.R.color.tt_*",
                  "*.R.style.tt_*",
                  "*.R.style.Theme.Dialog.TT_*",
                  "*.R.style.quick_*",
                  "*.R.style.EditTextStyle*",
                  "*.R.id.tt_*"
    ]
    obfuscatedBundleFileName = "compose_many.aab"
    // Obfuscated file name, must end with '.aab'
    mergeDuplicatedRes = false // Whether to allow the merge of duplicate resources
    enableFilterFiles = false // Whether to allow filter files
    filterList = [ // file filter rules
                   "META-INF/*"
    ]
    enableFilterStrings = false // switch of filter strings
    unusedStringPath = file("unused.txt").toPath() // strings will be filtered in this file
    languageWhiteList = [] // keep en,en-xx,zh,zh-xx etc. remove others.
}